plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion rootProject.ext.compile_sdk_version
    buildToolsVersion rootProject.ext.build_tools_version

    defaultConfig {
        applicationId rootProject.ext.application_id
        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.target_sdk_version
        versionCode rootProject.ext.version_code
        versionName rootProject.ext.version_name
        setProperty('archivesBaseName', applicationId + '-' + versionName)

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildFeatures {
        viewBinding = true
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    flavorDimensions 'environment'
    productFlavors {

        development {
            dimension 'environment'
            versionNameSuffix '-dev'
        }

        staging {
            dimension 'environment'
            versionNameSuffix '-staging'
        }

        production {
            dimension 'environment'
        }
    }
}

configurations {
    developmentImplementation
    stagingImplementation
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    // androidx
    implementation "androidx.core:core-ktx:$android_core_version"
    implementation "androidx.appcompat:appcompat:$android_appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$android_constraint_layout_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$android_nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$android_nav_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$android_hilt_version"
    kapt "androidx.hilt:hilt-compiler:$android_hilt_version"
    // google
    implementation "com.google.android.material:material:$android_material_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    // crash reporter for testing environment (not included for production)
    developmentImplementation "com.balsikandar.android:crashreporter:$crash_reporter_version"
    stagingImplementation "com.balsikandar.android:crashreporter:$crash_reporter_version"
    // glide bumptech
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    // intuit dimension collection
    implementation "com.intuit.ssp:ssp-android:$intuit_ssp_version"
    implementation "com.intuit.sdp:sdp-android:$intuit_sdp_version"
    // leak detector
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    // test
    testImplementation "junit:junit:$junit_version"
    testImplementation "io.mockk:mockk:$mockk_version"
}
